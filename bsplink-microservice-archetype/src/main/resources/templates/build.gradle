
buildscript {
	ext {
		springBootVersion = '2.0.0.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
    id "org.flywaydb.flyway" version "5.0.7"
  	id "io.franzbecker.gradle-lombok" version "1.12"
  	id "org.sonarqube" version "2.6"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'maven'


group = '@group@'
version = '@version@'
sourceCompatibility = 1.8

checkstyle {
    toolVersion = '8.0'
}

repositories {
	mavenCentral()
	maven {
        url nexusPublicRepositoryUrl
    }
}

dependencies {
	compile "org.springframework.boot:spring-boot-starter-data-jpa"
	compile "org.springframework.boot:spring-boot-starter-web"
	compile('org.springframework.boot:spring-boot-devtools')
    compile "io.springfox:springfox-swagger2:2.8.0"
    compile "io.springfox:springfox-swagger-ui:2.8.0"
    compile('commons-io:commons-io:2.4')
    compile "org.apache.commons:commons-lang:2.6"
	compile "org.jacoco:jacoco-maven-plugin:0.8.1"
	compile "org.flywaydb:flyway-core:5.0.7"
	runtime "com.h2database:h2"
	runtime('org.postgresql:postgresql')
	testCompile "org.springframework.boot:spring-boot-starter-test"
	testCompile('commons-io:commons-io:2.6')
}

compileJava.dependsOn(processResources)


// Integration tests
// See: http://buransky.com/programming/integration-testing-with-gradle/
configurations {
    itestCompile.extendsFrom testCompile
    itestRuntime.extendsFrom testRuntime
}

sourceSets {
    itest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java.srcDir file('src/itest/java')
    }
}

task itest(type: Test) {
    testClassesDir = sourceSets.itest.output.classesDir
    classpath = sourceSets.itest.runtimeClasspath
}

check.dependsOn itest
itest.mustRunAfter test

// Jacoco plugin configuration
jacoco {
	toolVersion = '0.8.0'
    //reportsDir = file("${buildDir}/reports/coverage")
}

jacocoTestReport {
	
    reports {
        csv.enabled false
        xml.enabled true
       	xml.destination "${buildDir}/reports/coverage/jacocoTestReport.xml"
        html.destination "${buildDir}/reports/coverage"
    }
    executionData += files("${buildDir}/jacoco/itest.exec")
}

jacocoTestCoverageVerification {
	
	//force Jacoco test coverage task to execute first 
	dependsOn jacocoTestReport
	
	//set the minimum coverage percentage allowed
    violationRules {
        rule {
            limit {
            	//TODO - set this value through a property?
                minimum = 1.0
            }
        }
    }
    executionData += files("${buildDir}/jacoco/itest.exec")
}

// to run Jacoco test coverage during the build (and fail when appropriate)
check {
	dependsOn jacocoTestCoverageVerification
}


// flyway plugin configuration
flyway {
    url = 'jdbc:h2:mem:testdb'
    user = 'sa'
    password = ''
}

// maven plugin configuration for uploading the generated artifact to Nexus
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: nexusSnapshotsRepositoryUrl){
                 authentication(userName: nexusUsername, password: nexusPassword)
            }
        }
    }
}
